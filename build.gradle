plugins {
    id 'java'
    id 'idea'
    id 'groovy'
    id 'signing'
    id 'java-library'
    id 'maven-publish'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.github.joutvhu'
version = '1.1.0'

def snapshotVersion = version.endsWith('-SNAPSHOT') || version.endsWith('.SNAPSHOT')

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

ext {
    versions = [
    ]
    managedVersions = [
    ]
    otherVersions = [
    ]
}

dependencyManagement {
    dependencies {
        ext.managedVersions.each {
            dependency "${it.key}:${ext.versions[it.value]}"
        }
    }
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.36'
    annotationProcessor 'org.projectlombok:lombok:1.18.36'

    testImplementation(platform('org.junit:junit-bom:5.12.1'))
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    testCompileOnly 'org.projectlombok:lombok:1.18.36'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.36'
}

jar {
    enabled = true
    manifest {
        attributes 'Built-By': 'joutvhu (Giao Ho)'
        attributes 'Build-Date': java.time.Instant.now().toString()
        attributes 'Bundle-Name': 'Xirr'
        attributes 'Bundle-Vendor': project.group
        attributes 'Bundle-SymbolicName': project.name
        attributes 'Bundle-Version': project.version
    }
    into("META-INF/maven/${project.group}/${project.name}") {
        from {
            generatePomFileForMavenPublication
        }
        rename {
            it.replace('pom-default.xml', 'pom.xml')
        }
    }
}

tasks.withType(JavaCompile) {
    options.release.set(8)
}

task fatJar(type: Jar) {
    manifest.from jar.manifest
    with jar
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives fatJar, sourcesJar, javadocJar
}

test {
    useJUnitPlatform()
    filter {
        includeTestsMatching 'com.joutvhu.xirr.*'
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = group
            artifactId = project.name
            artifacts = [fatJar, sourcesJar, javadocJar]
            version = version

            pom {
                name = project.name
                description = 'To calculate the internal rate of return for a schedule of cash flows that is not necessarily periodic.'
                url = 'https://github.com/joutvhu/xirr'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/joutvhu/xirr/blob/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'joutvhu'
                        name = 'Giao Ho'
                        email = 'joutvhu@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:joutvhu/xirr.git'
                    developerConnection = 'scm:git:git@github.com:joutvhu/xirr.git'
                    url = 'https://github.com/joutvhu/xirr'
                }
                issueManagement {
                    system = 'Github Issue'
                    url = 'https://github.com/joutvhu/xirr/issues'
                }
                organization {
                    name = 'Giao Ho'
                    url = 'https://github.com/joutvhu'
                }
                properties = project.ext.versions instanceof Map ? project.ext.versions : new HashMap<>()
                withXml {
                    def pomNode = asNode();
                    def dependencyManagement = pomNode.get('dependencyManagement')
                    if (dependencyManagement.dependencies.dependency.isEmpty()) {
                        pomNode.remove(dependencyManagement)
                    } else {
                        dependencyManagement.dependencies.dependency.each {
                            def key = "${it.groupId.text()}:${it.artifactId.text()}"
                            def versionProperty = project.ext.managedVersions[key]
                            if (versionProperty == null) {
                                versionProperty = project.ext.otherVersions[key]
                            }
                            if (versionProperty) {
                                it.version[0].setValue("\${$versionProperty}")
                            }
                        }
                    }

                    def dependenciesNode = pomNode.appendNode('dependencies')
                    if (configurations.implementation.allDependencies.isEmpty()) {
                        pomNode.remove(dependenciesNode)
                    } else {
                        configurations.implementation.allDependencies.each {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            if (it.version != null) {
                                dependencyNode.appendNode('version', it.version)
                            }
                        }
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = 'sonatype'
            if (snapshotVersion) {
                url = 'https://oss.sonatype.org/content/repositories/snapshots'
            } else {
                url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
            }
            credentials {
                username = project.ossrhToken
                password = project.ossrhTokenPassword
            }
        }
        maven {
            name = 'github'
            url = "https://maven.pkg.github.com/joutvhu/xirr"
            credentials {
                username = project.githubUsername
                password = project.githubPassword
            }
        }
    }
}

signing {
    sign publishing.publications.maven
}

tasks.publishMavenPublicationToGithubRepository.configure {
    onlyIf { !snapshotVersion }
}
